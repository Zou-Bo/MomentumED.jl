{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "865f57b8",
   "metadata": {},
   "source": [
    "### Filling Factor\n",
    "- **ν**: Filling factor 1/2\n",
    "- **Nk**: Number of k-points must be multiple of denominator for commensurability\n",
    "- **Ne = Nk * ν**: Number of electrons\n",
    "\n",
    "### Key Physics\n",
    "- **Ground state competition**: in n=0 Landau level, competition between pfaffin-related states and composite Fermi liquids; in n=1 Landau level, Moore-Read states are the ground states."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6c21cf16",
   "metadata": {},
   "outputs": [],
   "source": [
    "# These packages are not included in the MomentumED package. \n",
    "# Use the following line to add them:\n",
    "# using Pkg; Pkg.add(\"CairoMakie\"); Pkg.add(\"QuadGK\"); Pkg.add(\"ClassicalOrthogonalPolynomials\")\n",
    "\n",
    "# Import the momentum-conserved exact diagonalization package\n",
    "using MomentumED, LinearAlgebra\n",
    "include(\"../Landau level torus.jl\")\n",
    "using .LLT\n",
    "\n",
    "# square/triangular lattice\n",
    "sqa_lattice = ReciprocalLattice(:square);          # == ReciprocalLattice(1, 0)\n",
    "tri_lattice = ReciprocalLattice(:triangular);      # == ReciprocalLattice(1, -1//2)\n",
    "# general_lattice = ReciprocalLattice(abs(G2/G1), cos(θ))\n",
    "# use exact values, Integer or Rational, to avoid uncertainty on Brillouin Zone boundary.\n",
    "\n",
    "using CairoMakie # for plotting\n",
    "CairoMakie.activate!()\n",
    "\n",
    "# Plot the energy spectrum\n",
    "function plot_ed_spectrum(energies, ss_k1, ss_k2; \n",
    "    title = nothing, ylims = (nothing, nothing),\n",
    "    ylabel = \"Energy per unit cell (W₀ = e²/ϵl)\",\n",
    ")\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1, 1];\n",
    "        xlabel = \"$(Gk[2])k1+k2\",\n",
    "        ylabel = ylabel\n",
    "    )\n",
    "    ax_top = Axis(fig[1, 1];\n",
    "        xaxisposition = :top\n",
    "    )\n",
    "    top_ticks = ([], [])\n",
    "    hidespines!(ax_top)\n",
    "    hidexdecorations!(ax_top; label = false, ticklabels = false)\n",
    "    hideydecorations!(ax_top)\n",
    "    linkxaxes!(ax, ax_top)\n",
    "\n",
    "    # Plot energy levels for each momentum block\n",
    "    for i in 1:length(ss_k1)\n",
    "        x = Gk[2] * ss_k1[i] + ss_k2[i]\n",
    "        push!(top_ticks[1], x)\n",
    "        push!(top_ticks[2], string(i))\n",
    "        if isassigned(energies,i)\n",
    "            for e in energies[i]\n",
    "                scatter!(ax, x, e, color = :blue, marker=:hline)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    ylims!(ax, ylims...)\n",
    "    ax_top.xticks = top_ticks\n",
    "    if title isa String\n",
    "        ax_top.subtitle = title\n",
    "    end\n",
    "    display(fig)\n",
    "    fig\n",
    "end\n",
    "\n",
    "function plot_entanglement_spectrum(;title=nothing, ylims = (nothing, nothing))\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1,1];\n",
    "        xlabel = \"Ne in A-part\",\n",
    "        xticksvisible = false,\n",
    "        xgridvisible = false,\n",
    "        xminorgridvisible = true,\n",
    "    )\n",
    "\n",
    "    x = 0\n",
    "    NA_lines = Float64[-0.5;]\n",
    "    xtickposition = Float64[];\n",
    "    xticklabel = String[];\n",
    "    for indexNA in CartesianIndices(entanglement_spectrum)\n",
    "        x_begin = x\n",
    "        for (indexkA, spectrum) in enumerate(entanglement_spectrum[indexNA])\n",
    "            if !isempty(spectrum)\n",
    "                for e in spectrum\n",
    "                    scatter!(ax, x, e, color = :blue, marker=:hline)\n",
    "                end\n",
    "                x += 1\n",
    "            end\n",
    "        end\n",
    "        x_end = x\n",
    "        if x_end > x_begin\n",
    "            NA = string(indexNA[1]-1)\n",
    "            for i in 2:length(indexNA)\n",
    "                NA *= \",$(indexNA[i]-1)\"\n",
    "            end\n",
    "            push!(xtickposition, 0.5(x_begin+x_end-1))\n",
    "            push!(xticklabel, NA)\n",
    "            push!(NA_lines, x-0.5)\n",
    "        end\n",
    "    end\n",
    "    ax.xticks = (xtickposition, xticklabel)\n",
    "    ax.xminorticks = NA_lines\n",
    "    if title isa String\n",
    "        ax.subtitle = title\n",
    "    end\n",
    "    ylims!(ax, ylims...)\n",
    "    display(fig)\n",
    "    fig\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "14143281",
   "metadata": {},
   "outputs": [],
   "source": [
    "k_list = [0 1 2 3 0 1 2 3 0 1 2 3 0 1 2 3;\n",
    "          0 0 0 0 1 1 1 1 2 2 2 2 3 3 3 3]\n",
    "\n",
    "# System parameters\n",
    "Nk = 16         # Total number of k-points\n",
    "Gk = (4, 4)     # Grid dimensions (G1_direction, G2_direction)\n",
    "Ne = 6"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da96b38a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Set up component parameters: (layer, level, Chern number, pseudospin)\n",
    "sys_int = LandauInteraction(tri_lattice, (1, 0, 1, 0));\n",
    "\n",
    "# show the pseudopotential decomposition of the gate-screened Coulomb interaction for reference\n",
    "sys_int.D_l = 5.0                              # gate distance / magnetic length \n",
    "show( LLT.pseudo_potential_decomposition.(0:10; D_l = sys_int.D_l, d_l = 0.0) )\n",
    "sys_int.V_intra = LLT.pseudo_potential_decomposition.(0:10; D_l = sys_int.D_l, d_l = 0.0)\n",
    "# sys_int.V_intra[1:3] .= 0.0\n",
    "sys_int.V_intra = [0.0; 1.0]\n",
    "\n",
    "# use Haldane pseudopotential\n",
    "sys_int.mix = 0                  # mix * Haldane + (1-mix) * Coulomb\n",
    "\n",
    "# Create parameter structure for bilayer system\n",
    "para = EDPara(k_list = k_list, Gk = Gk, \n",
    "    V_int = sys_int, FF_inf_angle = LLT.Landau_ff_inf(sys_int));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6ba3861e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create momentum blocks (Hilbert subspace)\n",
    "subspaces, ss_k1, ss_k2 = ED_momentum_subspaces(para, (Ne, ));\n",
    "display(length.(subspaces))\n",
    "\n",
    "# one-body terms are all zero in flat Landau level\n",
    "scat = ED_sortedScatterList_twobody(para);\n",
    "\n",
    "# construct the Hamiltonian operator\n",
    "hmlt = MBOperator(scat; upper_hermitian = true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "23f747d3",
   "metadata": {},
   "outputs": [],
   "source": [
    "Neigen = 10  # Number of eigenvalues to compute per subspace\n",
    "energies = Vector{Vector{Float64}}(undef, length(subspaces));\n",
    "vectors = Vector{Vector{<:MBS64Vector}}(undef, length(subspaces));\n",
    "for i in eachindex(subspaces)\n",
    "    println(\"Processing subspace #$i with size $(length(subspaces[i])), momentum $(ss_k1[i]), $(ss_k2[i])\")\n",
    "    energies[i], vectors[i] = EDsolve(subspaces[i], hmlt;\n",
    "        N=Neigen, showtime=true, ishermitian=true\n",
    "    )\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fbecab16",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot_ed_spectrum(energies/Nk/LLT.W0, ss_k1, ss_k2,\n",
    "    title = \"Nk = $Nk, Ne = $Ne\",\n",
    ");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be18398b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# check energies explicitly to see degeneracy.\n",
    "\n",
    "sn = 2 # the subspace number to inspect\n",
    "println(\"total momentum: \", ss_k1[sn], \"  \", ss_k2[sn])\n",
    "energies[sn]/Nk/LLT.W0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "651be86a",
   "metadata": {},
   "source": [
    "Calculate the structure factor"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ed8c17eb",
   "metadata": {},
   "outputs": [],
   "source": [
    "NG = 2\n",
    "index_shift = NG .* Gk .+ 1\n",
    "densities = MBOperator[density_operator(q1, q2; \n",
    "        para = para, form_factor = true)\n",
    "    for q1 in -NG*Gk[1]:NG*Gk[1], q2 in -NG*Gk[2]:NG*Gk[2]\n",
    "];\n",
    "function structure_factor_expectation(myvec)\n",
    "    structure_factor = similar(densities, ComplexF64)\n",
    "    for q1 in -NG*Gk[1]:NG*Gk[1], q2 in -NG*Gk[2]:NG*Gk[2]\n",
    "        structure_factor[index_shift[1]+q1, index_shift[2]+q2] = \n",
    "            ED_bracket_threaded(myvec, \n",
    "                densities[index_shift[1]-q1, index_shift[2]-q2], \n",
    "                densities[index_shift[1]+q1, index_shift[2]+q2], myvec\n",
    "            )\n",
    "        if mod(q1, Gk[1]) == 0 && mod(q2, Gk[2])== 0\n",
    "            structure_factor[index_shift[1]+q1, index_shift[2]+q2] -= \n",
    "                ED_bracket_threaded(myvec, densities[index_shift[1]-q1, index_shift[2]-q2], myvec) * \n",
    "                ED_bracket_threaded(myvec, densities[index_shift[1]+q1, index_shift[2]+q2], myvec)\n",
    "        end\n",
    "    end\n",
    "    structure_factor ./= Nk\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e1fb148b",
   "metadata": {},
   "outputs": [],
   "source": [
    "sn = 4\n",
    "@time str_fac = structure_factor_expectation(vectors[sn][1]);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e1968f56",
   "metadata": {},
   "outputs": [],
   "source": [
    "let structure_factor = str_fac\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1,1]; aspect = DataAspect())\n",
    "    hm = heatmap!(ax, (-NG*Gk[1]:NG*Gk[1])./Gk[1], (-NG*Gk[2]:NG*Gk[2])./Gk[2], \n",
    "        real.(structure_factor);\n",
    "        colorrange = (0.0, maximum(real.(structure_factor))),\n",
    "        colormap = range(Makie.Colors.colorant\"white\", stop=Makie.Colors.colorant\"#ec2f41\", length=15)\n",
    "    )\n",
    "    Colorbar(fig[1, 2], hm)\n",
    "    display(fig)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "25b0d8c6",
   "metadata": {},
   "source": [
    "Compute the many-body connection and the Wilson loop for many-body Chern number."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "073f6799",
   "metadata": {},
   "outputs": [],
   "source": [
    "# path for the Wilson loop integral on base manifold\n",
    "# Landau level has uniform Berry curvature, so we compute Berry curvature of a small plaquate and then multiply plaquate number\n",
    "N_shift = 10  # number of shifts along each edge\n",
    "path = Tuple{Rational, Rational}[(0//1, 0//1)]\n",
    "push!(path, (1//N_shift, 0//1))\n",
    "push!(path, (1//N_shift, 1//N_shift))\n",
    "push!(path, (0//1, 1//N_shift))\n",
    "push!(path, (0//1, 0//1))\n",
    "path"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "073f6799",
   "metadata": {},
   "outputs": [],
   "source": [
    "sn = 6                     # subspace number\n",
    "nstates = 3                # number of degenerating states\n",
    "\n",
    "psi_before = vectors[sn][1:nstates]\n",
    "ED_connection_gaugefixing!(psi_before)  # fix global phase\n",
    "\n",
    "WilsonLoopIntegral= Vector{Float64}(undef, 4)\n",
    "for i in eachindex(WilsonLoopIntegral)\n",
    "\n",
    "    println(\"path point #$i \\t $(path[i+1])\")\n",
    "\n",
    "    scat_list = ED_sortedScatterList_twobody(para; kshift = path[i+1]);\n",
    "    psi_after = EDsolve(subspaces[sn], scat_list; N = 6,\n",
    "        showtime = false,\n",
    "    )[2][1:nstates]\n",
    "    ED_connection_gaugefixing!(psi_after)  # fix global phase\n",
    "\n",
    "    WilsonLoopIntegral[i] = ED_connection_step( \n",
    "        psi_after, psi_before, float.(path[i+1]), float.(path[i]), para;\n",
    "        wavefunction_tol = 1e-8, print_amp = true,\n",
    "        amp_warn_tol = 0.7, amp_warn = true\n",
    "    )\n",
    "\n",
    "    psi_before .= psi_after\n",
    "end\n",
    "ManyBodyChernNumber = sum(WilsonLoopIntegral) / (2π) * N_shift^2\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.1",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
