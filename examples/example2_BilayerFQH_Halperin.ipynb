{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This example demonstrates the calculation of bilayer fractional quantum Hall states using the Halperin (m,m',n) wavefunction.\n",
    "\n",
    "## Physical System\n",
    "- **Two-layer system**: Each layer can hold electrons with inter-layer tunneling\n",
    "- **Halperin (m,m',n) state**: Generalization of Laughlin state to bilayer systems. For (3,3,1) state, the filling factors are 1/4 for both layers.\n",
    "- **Inter-layer distance**: d\n",
    "- **Tunneling strength**: t\n",
    "- **Energy offset**: ΔE\n",
    "\n",
    "## Key Features\n",
    "- **Layer conservation**: Interaction conserves layer component of each scattering vertex (ci1=cf1, ci2=cf2)\n",
    "- **Layer-dependent interaction**: Extra e^{-ql*d_l} factor for different-layer interaction"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## System Parameters\n",
    "\n",
    "### Lattice Geometry\n",
    "- Triangular lattice with vectors G1 = (G, 0), G2 = (-G/2, sqrt(3)*G/2)\n",
    "- Landau level n=0\n",
    "- Brillouin zone area = 2π/l² = sqrt(0.75) * G²\n",
    "\n",
    "### Bilayer Parameters\n",
    "- **Nc = 2**: Number of layers (components)\n",
    "- **Inter-layer distance**: d/l\n",
    "- **Tunneling amplitude**: t / W0, if t=0, layer becomes conserved\n",
    "- **Layer conservation**: Interactions preserve layer indices\n",
    "\n",
    "### Interaction Details\n",
    "- **Same-layer interaction**: Identical to single-layer case\n",
    "- **Different-layer interaction**: Includes e^{-ql*d/l} attenuation factor\n",
    "- **Form factor**: V(q) = W0 * 1/|ql| * tanh(|qD|) "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Import the momentum-conserved exact diagonalization package\n",
    "using MomentumED\n",
    "\n",
    "# Number of layers (components) for bilayer system\n",
    "Nc_hopping = 2\n",
    "\n",
    "# Define k-mesh for bilayer system (2×3 mesh, Nk=6) in triangular lattice\n",
    "# Using a smaller mesh for computational efficiency\n",
    "k_list = [0 1 2 3 0 1 2 3 0 1 2 3;\n",
    "          0 0 0 0 1 1 1 1 2 2 2 2]\n",
    "Nk = 12\n",
    "Gk = (4, 3)  # Grid dimensions\n",
    "\n",
    "# Total number of electrons (3 electrons across both layers)\n",
    "Ne = 6"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Physical parameters\n",
    "Gl = sqrt(2π/sqrt(0.75))   # Magnetic length scale\n",
    "D_l = 5.0                  # Screening length D/l\n",
    "W0 = 1.0                   # Interaction strength\n",
    "G12_angle = 2π/3           # Angle between reciprocal lattice vectors (triangular lattice)\n",
    "d_l = 0.1                  # Inter-layer distance d/l\n",
    "t = 0.0 * W0              # Inter-layer tunneling strength\n",
    "ΔE = 0 * W0              # Energy difference between the two layers\n",
    "\n",
    "# Set up one-body Hamiltonian matrix for inter-layer tunneling\n",
    "# H_onebody[c1, c2, cc, k] : hopping from component c2 to c1 at momentum k\n",
    "# For bilayer system: Nc_hopping=2, Nc_conserve=1, Nk=6\n",
    "H_onebody_bilayer = zeros(ComplexF64, Nc_hopping, Nc_hopping, 1, Nk)\n",
    "\n",
    "# Add tunneling terms: t = 0.5 * W0 between different layers\n",
    "# Tunneling conserves momentum (diagonal in k-space)\n",
    "for k_idx in 1:Nk\n",
    "    # Hopping from layer 2 to layer 1\n",
    "    H_onebody_bilayer[1, 2, 1, k_idx] = t\n",
    "    # Hopping from layer 1 to layer 2  \n",
    "    H_onebody_bilayer[2, 1, 1, k_idx] = t\n",
    "    H_onebody_bilayer[1, 1, 1, k_idx] = 0.5ΔE\n",
    "    H_onebody_bilayer[2, 2, 1, k_idx] = -0.5ΔE\n",
    "end\n",
    "\n",
    "\n",
    "# Define the form factor for Coulomb interaction in Landau level\n",
    "# This includes the Landau level projection exp(-0.5 * q²l²)\n",
    "# For bilayer: add extra e^{-ql*d/l} factor for different-layer interactions\n",
    "function VFF_bilayer(q1::Float64, q2::Float64; diff_layer=false)\n",
    "    ql = sqrt(q1^2 + q2^2 + 2cos(G12_angle) * q1*q2) * Gl\n",
    "    if ql == 0.0\n",
    "        return W0 * D_l  # Regularization at q=0\n",
    "    end\n",
    "    \n",
    "    V = W0 / ql * tanh(ql * D_l) * exp(-0.5 * ql^2)\n",
    "    \n",
    "    # Apply inter-layer attenuation factor for different-layer interactions\n",
    "    if diff_layer\n",
    "        return V * exp(-ql * d_l)  # Exponential attenuation with distance d_l\n",
    "    end\n",
    "    \n",
    "    return V\n",
    "end\n",
    "\n",
    "# Sign function for reciprocal lattice vectors\n",
    "# Determines the phase structure of the magnetic translation group\n",
    "function ita(g1::Int64, g2::Int64)\n",
    "    if iseven(g1) && iseven(g2)\n",
    "        return 1\n",
    "    else\n",
    "        return -1\n",
    "    end\n",
    "end\n",
    "\n",
    "# Cross product for 2D vectors (returns scalar)\n",
    "# Used for computing geometric phases in magnetic field\n",
    "function ql_cross(q1_1, q1_2, q2_1, q2_2)\n",
    "    return q1_1 * q2_2 - q1_2 * q2_1\n",
    "end\n",
    "\n",
    "# Bilayer interaction function - same structure as single-layer but with layer-dependent VFF\n",
    "function V_int_bilayer(kf1, kf2, ki2, ki1, cf1=1, cf2=1, ci2=1, ci1=1; output=false)::ComplexF64\n",
    "    \n",
    "    # Layer conservation: interaction must conserve layer indices\n",
    "    if ci1 != cf1 || ci2 != cf2\n",
    "        return 0.0 + 0.0im\n",
    "    end\n",
    "    \n",
    "    \n",
    "    # Calculate momentum transfer (modulo reciprocal lattice)\n",
    "    q = rem.(ki1 .- kf1, 1, RoundNearest)\n",
    "    G_shift1 = round.(Int64, ki1 .- kf1 .- q, RoundNearest)\n",
    "    G_shift2 = round.(Int64, kf2 .- ki2 .- q, RoundNearest)\n",
    "\n",
    "\n",
    "    V_total = ComplexF64(0.0)\n",
    "    # Sum over reciprocal lattice vectors for convergence\n",
    "    Nshell = 2\n",
    "    for g1 in -Nshell:Nshell, g2 in -Nshell:Nshell\n",
    "        if abs(g1-g2) > Nshell\n",
    "            continue\n",
    "        end\n",
    "\n",
    "        # Construct the full momentum transfer including reciprocal lattice\n",
    "        qq1 = q[1] + g1\n",
    "        qq2 = q[2] + g2\n",
    "\n",
    "        # Calculate phase factors from magnetic translation algebra\n",
    "        # These phases ensure proper commutation relations and gauge invariance\n",
    "        phase_angle = 0.5ql_cross(ki1[1], ki1[2], kf1[1], kf1[2])\n",
    "        phase_angle += 0.5ql_cross(ki1[1]+kf1[1], ki1[2]+kf1[2], qq1, qq2)\n",
    "        phase_angle += 0.5ql_cross(ki2[1], ki2[2], kf2[1], kf2[2])\n",
    "        phase_angle += 0.5ql_cross(ki2[1]+kf2[1], ki2[2]+kf2[2], -qq1, -qq2)\n",
    "\n",
    "        phase = cispi(2.0phase_angle)\n",
    "        sign = ita(g1+G_shift1[1], g2+G_shift1[2]) * ita(g1+G_shift2[1], g2+G_shift2[2])\n",
    "\n",
    "        # Use layer-dependent form factor\n",
    "        vff = VFF_bilayer(qq1, qq2; diff_layer=(ci1 != ci2))\n",
    "        V_total += sign * phase * vff\n",
    "    end\n",
    "\n",
    "    return V_total\n",
    "end\n",
    "\n",
    "# Define the Landau level infinitesimal form factor\n",
    "function Landau_ff_inf(k_f, k_i, c)\n",
    "    dk = k_f .- k_i\n",
    "    k = 0.5 .* (k_f .+ k_i)\n",
    "    return -π * (k[1]*dk[2] - k[2]*dk[1])\n",
    "end\n",
    "\n",
    "# Create parameter structure for bilayer system\n",
    "# Note: H_onebody matrix handles tunneling, V_int handles interactions\n",
    "para_bilayer = EDPara(\n",
    "    k_list=k_list, \n",
    "    Gk=Gk, \n",
    "    Nc_hopping=Nc_hopping,\n",
    "    Nc_conserve=1,\n",
    "    H_onebody=H_onebody_bilayer,\n",
    "    V_int=V_int_bilayer,\n",
    "    FF_inf_angle=Landau_ff_inf,\n",
    ");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create momentum blocks for bilayer system\n",
    "blocks, block_k1, block_k2, k0number = \n",
    "    ED_momentum_block_division(para_bilayer, ED_mbslist(para_bilayer, (Ne,)));\n",
    "length.(blocks)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate scattering lists for efficient Hamiltonian construction\n",
    "scat_list1 = ED_sortedScatteringList_onebody(para_bilayer);\n",
    "scat_list2 = ED_sortedScatteringList_twobody(para_bilayer);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Neigen = 6\n",
    "energies = Vector{Vector{Float64}}(undef, length(blocks))\n",
    "vectors = Vector{Vector{Vector{ComplexF64}}}(undef, length(blocks))\n",
    "for i in eachindex(blocks)\n",
    "    println(\"Processing block #$i with size $(length(blocks[i])), momentum $(block_k1[i]), $(block_k2[i])\")\n",
    "    energies[i], vectors[i] = EDsolve(blocks[i], scat_list2, scat_list1; \n",
    "        N = Neigen, showtime=true,\n",
    "    )\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# The package for plotting is not included in this package. Use the following to add it:\n",
    "# using Pkg; Pkg.add(\"CairoMakie\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot energy spectrum\n",
    "using CairoMakie\n",
    "CairoMakie.activate!()\n",
    "\n",
    "begin\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1, 1])\n",
    "    # Plot energy levels for each momentum block\n",
    "    for i in 1:length(blocks)\n",
    "        for e in energies[i]\n",
    "            scatter!(ax, i, e, color = :blue, marker=:hline)\n",
    "        end\n",
    "    end\n",
    "    xlims!(-0.2, 1.2+length(blocks))\n",
    "    fig\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# check energies explicitly to see if there's degeneracy in the same momentum sector.\n",
    "bn = 4 # the block number to inspect\n",
    "energies[bn]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot the reduced density matrix of the ground eigenstate in the first block\n",
    "rdm = ED_onebody_rdm(blocks[bn], vectors[bn][1])\n",
    "begin\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1, 1]; yreversed = true)\n",
    "    hm = heatmap!(ax, abs.(rdm); colorrange = (0,1), \n",
    "        colormap = range(Makie.Colors.colorant\"white\", stop=Makie.Colors.colorant\"#ec2f41\", length=15)\n",
    "    )\n",
    "    Colorbar(fig[1, 2], hm)\n",
    "    fig\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the specific case when interlayer tunneling t=0, particle numbers of the two layers are conserved.\n",
    "\n",
    "With this, we can do larger system."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Import the momentum-conserved exact diagonalization package\n",
    "using MomentumED\n",
    "\n",
    "# Number of layers (components) for bilayer system\n",
    "Nc_conserve = 2\n",
    "\n",
    "# Define k-mesh for bilayer system (2×3 mesh, Nk=6) in triangular lattice\n",
    "# Using a smaller mesh for computational efficiency\n",
    "k_list = [0 1 2 3 0 1 2 3 0 1 2 3 0 1 2 3;\n",
    "          0 0 0 0 1 1 1 1 2 2 2 2 3 3 3 3]\n",
    "Nk = 16\n",
    "Gk = (4, 4)  # Grid dimensions\n",
    "\n",
    "# Total number of electrons (3 electrons across both layers)\n",
    "Ne1 = 4\n",
    "Ne2 = 4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Physical parameters\n",
    "t = 0.0                    # must be zero for layer-conserved case\n",
    "ΔE = 0 * W0                # Energy difference between the two layers\n",
    "\n",
    "\n",
    "# Set up one-body Hamiltonian matrix for inter-layer tunneling\n",
    "# H_onebody[c1, c2, cc, k] : no hopping from component c2 to c1\n",
    "# For bilayer system: Nc_hopping=1, Nc_conserve=2, Nk=6\n",
    "H_onebody_bilayer = zeros(ComplexF64, 1, 1, Nc_conserve, Nk)\n",
    "# Add energy difference terms: ΔE = 0.5 * W0 between different layers\n",
    "# No tunneling\n",
    "for k_idx in 1:Nk\n",
    "    H_onebody_bilayer[1, 1, 1, k_idx] = 0.5ΔE\n",
    "    H_onebody_bilayer[1, 1, 2, k_idx] = -0.5ΔE\n",
    "end\n",
    "\n",
    "# Create parameter structure for bilayer system\n",
    "# Note: H_onebody matrix handles tunneling, V_int handles interactions\n",
    "para_bilayer = EDPara(\n",
    "    k_list=k_list, \n",
    "    Gk=Gk, \n",
    "    Nc_hopping=1,\n",
    "    Nc_conserve=Nc_conserve,\n",
    "    H_onebody=H_onebody_bilayer,\n",
    "    V_int=V_int_bilayer\n",
    ");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create momentum blocks for bilayer system\n",
    "blocks, block_k1, block_k2, k0number = \n",
    "    ED_momentum_block_division(para_bilayer, ED_mbslist(para_bilayer, (Ne1,Ne2)));\n",
    "length.(blocks)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate scattering lists for efficient Hamiltonian construction\n",
    "scat_list1 = ED_sortedScatteringList_onebody(para_bilayer);\n",
    "scat_list2 = ED_sortedScatteringList_twobody(para_bilayer);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Time cost is significantly higher with this larger system size\n",
    "# use tol=W0*1e-6 to speed up convergence\n",
    "energies = Vector{Vector{Float64}}(undef, length(blocks))\n",
    "Neigen = 6\n",
    "for i in eachindex(blocks)\n",
    "    println(\"Processing block #$i with size $(length(blocks[i])), momentum $(block_k1[i]), $(block_k2[i])\")\n",
    "    if i == k0number\n",
    "        energies[i] = EDsolve(blocks[i], scat_list1, scat_list2, 2Neigen; showtime=true, converge_warning = false, tol=W0*1e-6)[1]\n",
    "    else\n",
    "        energies[i] = EDsolve(blocks[i], scat_list1, scat_list2, Neigen; showtime=true, converge_warning = false, tol=W0*1e-6)[1]\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# The package for plotting is not included in this package. Use the following to add it:\n",
    "# using Pkg; Pkg.add(\"CairoMakie\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot energy spectrum\n",
    "using CairoMakie\n",
    "CairoMakie.activate!()\n",
    "\n",
    "begin\n",
    "    fig = Figure();\n",
    "    ax = Axis(fig[1, 1])    \n",
    "    # Plot energy levels for each momentum block\n",
    "    for i in 1:length(blocks)\n",
    "        for e in energies[i]\n",
    "            scatter!(ax, Gk[2]*block_k1[i]+block_k2[i], e, color = :blue, marker=:hline)\n",
    "        end\n",
    "    end\n",
    "    fig\n",
    "end\n",
    "display(fig)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# check energies explicitly to see if there's degeneracy in the same momentum sector.\n",
    "energies[1]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
